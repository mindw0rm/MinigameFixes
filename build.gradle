import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.plajer.xyz/minecraft' } // Village Defense
    maven { url 'https://dl.bintray.com/a5h73y/repo/' } // Parkour
    maven { url "https://maven.enginehub.org/repo/" }  // Worldguard
}

group = project.property("group")
version = project.property("version")
sourceCompatibility = project.property("sourceversion")

println("### Target is ${project.name}-${version}.jar")

dependencies {
    // SPigot API
    implementation group: 'org.spigotmc', name: 'spigot-api', version: project.property("apiversion")
    // plugins
    implementation group: 'pl.plajer', name: 'villagedefense', version: '4.4.2'
    implementation group: 'me.A5H73Y', name: 'Parkour', version: '5.3'

    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.4-SNAPSHOT'

    // annotation handling
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    annotationProcessor group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
}

processResources {
    filter ReplaceTokens, tokens: [
            "apiversion": project.property("apiversion"),
            "version"   : project.property("version")
    ]
}

task mkLatest(type: Copy, dependsOn: ['build']) {
    from "${buildDir}/libs/${project.name}-${version}.jar"
    into "final/"
    rename "${project.name}-${version}.jar", "${project.name}-LATEST.jar"
}

task deploy(type: Copy, dependsOn: ['build', 'mkLatest']) {
    from "${buildDir}/libs"
    into "final/"
}